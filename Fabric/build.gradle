plugins {
	id 'fabric-loom' version '0.12-SNAPSHOT'
}

archivesBaseName = "${project.mod_name}-fabric-${project.minecraft_version}"

repositories {
	maven {
		url = "https://maven.siphalor.de"
		content { includeGroup "de.siphalor" }
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings loom.officialMojangMappings()
	modImplementation "net.fabricmc:fabric-loader:${project.fabric_loader_version}"
	modImplementation fabricApi.module("fabric-key-binding-api-v1", project.fabric_api_version)
	
	//xplat
	implementation project(":Xplat")
	
	//integration: AMECS API (see: https://github.com/Siphalor/nmuk/issues/9 for reasoning behind the excludes)
	modCompileOnly("de.siphalor:${project.amecsapi_artifactid}:${project.amecsapi_version}") { exclude group: "com.github.astei" }
	
	//trick out the local workspace a bit
	modLocalRuntime fabricApi.module("fabric-resource-loader-v0", project.fabric_api_version)
	
	//FABRIC NETWORKING API REFMAPS ARE FUCKING BROKEN OR WHATEVER, AMECS PULLS IN ALL OF FABRIC API, I DONT GIVE ENOUGH OF A SHIT TO DEBUG THIS
//	modLocalRuntime("de.siphalor:${project.amecs_artifactid}:${project.amecs_version}") {
//		exclude group: "com.github.astei"
//		exclude module: project.nmuk_artifactid //for testing amecs w/o nmuk
//	}
	//modLocalRuntime("de.siphalor:${project.nmuk_artifactid}:${project.nmuk_version}") { exclude group: "com.github.astei"	} //for testing nmuk w/o amecs
}

//fold in xplat resources and sources
processResources {
	from project(":Xplat").sourceSets.main.resources
}

tasks.withType(JavaCompile) {
	source(project(":Xplat").sourceSets.main.allSource)
}

//loom doesn't generate run configs by default in subprojects
loom {
	runs {
		client {
			client()
			setConfigName("Fabric Client")
			ideConfigGenerated(true)
			runDir("./run")
		}
		server {
			server()
			setConfigName("Fabric Server")
			ideConfigGenerated(true)
			runDir("./run")
		}
	}
	
	//nor can it figure out the refmap name in this situation
	mixin {
		defaultRefmapName = "${project.mod_name}.refmap.json"
	}
}